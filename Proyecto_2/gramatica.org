* Gram√°tica

#+BEGIN_EXAMPLE

file_input: (SALTO | stmt)* ENDMARKER

stmt: simple_stmt | compound_stmt
simple_stmt: small_stmt SALTO
small_stmt: expr_stmt | print_stmt
expr_stmt: test '=' test
print_stmt: 'print' test

compound_stmt: if_stmt
             | while_stmt
if_stmt: 'if' test ':' suite ['else' ':' suite]
while_stmt: 'while' test ':' suite
suite: simple_stmt | SALTO INDENTA stmt+ DEINDENTA

test: or_test
or_test: and_test ('or' and_test)*
and_test: not_test ('and' not_test)*
not_test: 'not' not_test | comparison
comparison: expr (comp_op expr)*
comp_op: '<'|'>'|'=='|'>='|'<='|'!='
expr: term (('+'|'-') term)*
term: factor (('*'|'/'|'%'|'//') factor)*
factor: ('+'|'-') factor | power
power: atom ['**' factor]
atom: IDENTIFICADOR | ENTERO | CADENA
    | REAL | BOOLEANO | '(' test ')'

#+END_EXAMPLE
